#!/usr/bin/env just --justfile
# https://github.com/casey/just

build-jar :
	./gradlew jar



dependencies-tree :
	./gradlew dependencies --configuration testCompileOnly

extract-all-jars :
	rm -rf jar-files
	./gradlew extractAllJars

super-files :
	rm -rf super-files
	just dependencies-tree
	just extract-all-jars
	mv -v jar-files super-files
	fd -uu --search-path=super-files --type=file --fixed-strings lint.jar --exec rm -rfv
	tree -N -d super-files

jar-files :
	just dependencies-tree
	just extract-all-jars
	fd -uu --search-path=super-files --type=directory --max-depth=1 --exec rm -rf "jar-files/{/}"
	tree -N -d jar-files



reset-out :
	rm -rfv out

reset-out-android :
	mkdir -p out
	touch out/android.d.ts
	truncate -s0 out/android.d.ts

out-each :
	just reset-out
	for i in jar-files/*; do just out "$(basename "$i")"; done
	tree -N out

out input :
	just reset-out-android
	java -jar build/libs/dts-generator.jar \
		-skip-declarations \
		-super $ANDROID_HOME/platforms/android-29/android.jar \
		-super super-files \
		-super jar-files \
		-input-generics ../libs/generics.txt \
		-input "jar-files/{{input}}"
	mv -v out/android.d.ts "out/$(echo "{{input}}" | grep --only-matching "(.*)" | tr -d "()").d.ts"
# mv -v out/android.d.ts "out/$(echo "{{input}}" | tr -cs "[:alpha:]" "_").d.ts"

out-all :
	just reset-out
	just reset-out-android
	java -jar build/libs/dts-generator.jar \
		-skip-declarations \
		-super $ANDROID_HOME/platforms/android-29/android.jar \
		-super super-files \
		-super jar-files \
		-input-generics ../libs/generics.txt \
		-input jar-files



watch-src :
	watchexec --watch src --restart -- 'clear; just build-jar; just out-all'



android-platform version :
	just reset-out
	just reset-out-android
	java -jar build/libs/dts-generator.jar \
		-input-generics ../libs/generics.txt \
		-input $ANDROID_HOME/platforms/android-{{version}}/android.jar
	mv -v out/android.d.ts ../out/android-platform-{{version}}.d.ts

androidx version :
	just reset-out
	just reset-out-android
	java -jar build/libs/dts-generator.jar \
		-skip-declarations \
		-super $ANDROID_HOME/platforms/android-{{version}}/android.jar \
		-input-generics ../libs/generics.txt \
		-input super-files
	mv -v out/android.d.ts ../out/androidx-{{version}}.d.ts
